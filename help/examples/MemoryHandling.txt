{
  use macro (Ctrl+J) "headsa"
  to insert a file header      
}
{$VERSION 3.1.0000}

thread 'MAIN'
var              
 $PLAYER_CHAR: Player 
end // var
01F0: set_max_wanted_level_to 6 
set_wb_check_to 0 
00C0: set_current_time 8 0 
04E4: unknown_refresh_game_renderer_at 2288.6433 -1157.5918 
Camera.SetAtPos(2288.6433, -1157.5918, 26.6636)
$PLAYER_CHAR = Player.Create(#NULL, 2288.6433, -1157.5918, 26.6636)
$PLAYER_ACTOR = Actor.EmulateFromPlayer($PLAYER_CHAR)
07AF: $PLAYER_GROUP = player $PLAYER_CHAR group
Camera.SetBehindPlayer
set_weather 0          
wait 0 ms                                
$PLAYER_CHAR.SetClothes("PLAYER_FACE", "HEAD", Head)
$PLAYER_CHAR.SetClothes("JEANSDENIM", "JEANS", Legs)
$PLAYER_CHAR.SetClothes("SNEAKERBINCBLK", "SNEAKER", Shoes)
$PLAYER_CHAR.SetClothes("VEST", "VEST", Torso)
$PLAYER_CHAR.Build
$PLAYER_CHAR.CanMove = True
fade 1 (out) 0 ms                     
select_interior 0
0629: change_stat 181 (islands unlocked) to 4
016C: restart_if_wasted at 2027.77 -1420.52 15.99 angle 137.0 town_number 0
016D: restart_if_busted at 1550.68 -1675.49 14.51 angle 90.0 town_number 0
0180: set_on_mission_flag_to $ONMISSION // Note: your missions have to use the variable defined here ($ONMISSION)
03E6: remove_text_box

// example 1
create_thread @LOWGRAVITY

// example 2 
create_thread @LONGTRAINS

// example 3
create_thread @NEWCHEATS

// example 4
create_thread @TEST1
create_thread @CHANGELOCALVAR

// example 5
create_thread @MSGREMOVE

end_thread 

{

                 SA MEMORY HANDLING ROUTINES
                 TESTED ON SAN ANDREAS US v1.0
               
                 MAY NOT WORK WITH OTHER VERSIONS
  
}

{ ------------------------------------------------------------
           Example 1
           Change the game gravity
          
           WORKS ONLY IF YOU HAVE INSTALLED XIEON'S SA MEMPATCH
           TOOLS\SA MEMORY PATCH\SCRIPTPATCH.EXE

 ------------------------------------------------------------ }
:LOWGRAVITY
thread 'GRAVITY'
// WriteMem(0x863984, 0.0015, 4, True)
end_thread


{ ------------------------------------------------------------
           Example 2
           Make extremely long trains, 15+ carriages!
         
           Game address: 0x8D44F8
           http://www.gtaforums.com/index.php?showtopic=194199&view=findpost&p=3941357
 ------------------------------------------------------------ }
:LONGTRAINS
thread 'TRAINS' 
   for 0@ = -382229 to -382216
     wait 0 
     &0(0@,1i) = #STREAKC        
   end
                                               
   // type0 changed!

   // load models
   #FREIGHT.Load
   #FREIFLAT.Load
   #STREAKC.Load

   while true
     if and
       Model.Available(#FREIGHT)
       Model.Available(#FREIFLAT)
       Model.Available(#STREAKC)   
     then 
       Break
     end
     wait 0
   end
     
   // create train with new carriages 
   06D8: 1@ = create_train_at 2278.1771 -1144.8823 27.5108 type 0 direction 1 

   #FREIGHT.Destroy
   #FREIFLAT.Destroy
   #STREAKC.Destroy
   
end_thread

{ ------------------------------------------------------------
           Example 3
           New cheats in run-time
         
           Game address: 0x969110
           see: 
           http://www.gtaforums.com/index.php?showtopic=228037&view=findpost&p=3943966
 ------------------------------------------------------------ }


:NEWCHEATS
// EXAMPLE 1: TEST 1 key press (space)
thread 'CHEATS'
0@ = -229908
while true
 008B: 1@ = &0(0@,1i)      // get last keypresses
 0085: 2@ = 1@         
 div(1@, 0x 1 00)          // 1char: 0x100, 2chars: 0x10000: 3chars: 0x1000000
 mul(1@, 0x 1 00)          // same
 0062: 2@ -= 1@            // get needed number of chars (1)
 
 if
   2@ == 0x20              // test if it's SPACE
 then  
    03E5: text_box 'CHEAT1'// Cheat activated
    Break
 end
 wait 1000 
end 

// EXAMPLE 2: TEST 2 keys ('NO')
0@ = -229908
while true
 008B: 1@ = &0(0@,1i)       // get last keypresses
 0085: 2@ = 1@         
 div(1@, 0x 1 00 00)        // 1char: 0x100, 2chars: 0x10000: 3chars: 0x1000000
 mul(1@, 0x 1 00 00)        // same
 0062: 2@ -= 1@             // get needed number of chars (2)
 if
   2@ == 0x4e4f             // test if player typed NO
 then   
   03E5: text_box 'CHEAT1'  // Cheat activated
   Break
 end
 wait 1000 
end 

// EXAMPLE 3: TEST 3 keys ('WOW')
0@ = -229908
while true
 008B: 1@ = &0(0@,1i)       // get last keypresses
 0085: 2@ = 1@         
 div(1@, 0x 1 00 00 00)     // 1char: 0x100, 2chars: 0x10000: 3chars: 0x1000000
 mul(1@, 0x 1 00 00 00)     // same
 0062: 2@ -= 1@             // get needed number of chars  (3)
 if
   2@ == 0x574f57           // test if player typed WOW
 then
     03E5: text_box 'CHEAT1'// Cheat activated
   Break
 end
 wait 1000 
end 


// EXAMPLE 4: TEST 4 keys ('HACK')
0@ = -229908
while true
 if
   &0(0@,1i) == 0x4841434B  // test if player typed HACK
 then
   03E5: text_box 'CHEAT1'  // Cheat activated
   Break
 end
 wait 1000 
end


// EXAMPLE 5: TEST 5 keys ('SANNY')

// test 5th char 's' from address +4b, then test 'anny' from the beginning;

//  addr      keys
// -229908: X X X X    |  -229908: A N N Y
// -229907: X X X O <- |  -229907: _ _ _ S                    

while true
 0@ = -229907 // +32bits; next 4 bytes/chars in the last keypresses block
 008B: 1@ = &0(0@,1i)
 0085: 2@ = 1@         
 div(1@, 0x100)
 mul(1@, 0x100)
 0062: 2@ -= 1@               // 5th char is the last in the second block:
 if
   2@ == 0x53                 // test if S is the 5th pressed key 
 then                         
    0@ = -229908
    008B: 3@ = &0(0@,1i)
    if 3@ == 0x414E4E59       // test if player also typed ANNY after that 
    then  
      03E5: text_box 'CHEAT1' // Cheat activated
      Break
    end     
 end
 wait 1000 
end

     
// EXAMPLE 6: TEST 16 keys: '1234567812345678'
0@ = -57477
while true
 if
   &0(0@,1v) == "8765432187654321" // test if last pressed key combo is "1234567812345678"
 then
   03E5: text_box 'CHEAT1'    // Cheat activated
   Break
 end
 wait 1000 
end

end_thread
  
{ ------------------------------------------------------------
             Example 4
             Change local variables of any thread
           
             Game address: 0xA8B42C
             see: 
             http://forums.ag.ru/?board=gta&action=display&num=1057863893&start=2405#2405 (in Russian)
 ------------------------------------------------------------ }

// ---------------------------------------------
//  This thread searches the one named TEST
//  and changes its local variable 10@
// ---------------------------------------------

:CHANGELOCALVAR
thread 'CLV'
 {
   0@  =  thread address
   1@  =  temp
 }
 0@ = 0xA8B42C  
 // FIND_THREAD_LOOP
 while true
   gosub @MemoryRead32
   // 1@ = first active thread
   if 1@ == 0
   then
     Break // no threads, search failed
   end
   0085: 0@ = 1@    // save the pointer
   // get thread name magic address 
   div(1@, 8)
   dec(1@, 1 348 395)
   {
    IMPORTANT NOTE:
     thread names are stored in lowercase registry,
     so we've to compare it in lowercase as well.
     SB option 'Case Converting' has to be set to 'As is'!
   } 
   if &0(1@,1s) == 'test' 
   // check if this thread's name is "test"
   then    
     // well, we've found it, can do everything with it
     // 0@ contains that thread address

     // get address of 10@      
     inc(0@, 0x3c)     // thread locals pool
     inc(0@, 40)       // local var name * 4; i.e. set to 36 to change 9@
    
     // MEMORY WRITE DWORD
     1@ = 3333 // new value of thread('test').10@
     gosub @MemoryWrite32

     Break // end the LOOP
   else
     // no, that thread has another name
     // check the next one
     wait 0
   end
   // go to while_begin
 end
 // variable changed, end_thread   
end_thread

// ---------------------------------------------
//  This thread shows a number after 1 sec
//  after activated. The number is stored in 10@
// ---------------------------------------------
:TEST1
thread 'TEST' 
 10@ = 10000
 wait 3000 
 054C: use_GXT_table 'POOL' 
 01E3: text_1number_styled 'NUM' 10@ 5000 ms 1  // ~1~
end_thread



{ ------------------------------------------------------------
           Example 5
           Remove the message "To stop Carl..." when the player
           first time stealing a car
         
           Game address: 0xC0BC15
           see: 
           http://www.gtaforums.com/index.php?showtopic=257379&view=findpost&p=3928052
 ------------------------------------------------------------ }

:MSGREMOVE
  thread 'NOMSG'
  0@ = 0xC0BC15 //  ADDRESS
  2@ = 1        //  VALUE
  3@ = 1        //  LENGTH (Byte)
  gosub @MemoryWrite
end_thread


// --------------------------------------------------------------------
{
    Memory Handling Routines (san andreas)
  
   1 MemoryWrite: write new value with specified length into the memory
    Params: 0@ = address; 2@ = new value; 3@ - value length (1, 2, 3, 4) 
  
   2 MemoryWrite_DWORD: write new DWord value into the memory
    Params: 0@ = address; 1@ = new value;
  
   3 MemoryRead: read DWord value from the memory
    Params: 0@ = address; 1@ = returned value;
  
    note that some address could be unreadable/unrewriteable!!!
    to change such addresses try Xieon's MemPatch: ..\tools\Sa Memory Patch\
}
// --------------------------------------------------------------------

//--write specified number of bytes into the memory

:MemoryWrite   
 0085: 5@ = 0@
 0@ /= 4
 0@ *= 4             // memory address
 0062: 5@ -= 0@      // offset (0, 1, 2, 3)  
:_GetInitValue       // if you specify mem offset in 5@, you're able to gosub here
 gosub @MemoryRead32 // get initial value
 3@ *= 8 // bytes -> bits
 5@ *= 8
 dec(3@)
 for 6@ = 0 to 3@
   if
     08B6: test 2@ bit 6@ 
   then
     08BF: set 1@ bit 5@    // 1
   else
     08C5: clear 1@ bit 5@  // 0
   end 
   inc(5@) // next memory bit
 end    
 008A: &0(0@,1i) = 1@  // write new value 
return

//--write 32-bit value into the memory-----------

:MemoryWrite32
 0@ -= 0xA49960
 0@ /= 4
 008A: &0(0@,1i) = 1@   
return

//--read 32-bit value from the memory-----------

:MemoryRead32
 0@ -= 0xA49960
 0@ /= 4
 008B: 1@ = &0(0@,1i)
return



